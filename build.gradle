buildscript {
    repositories {
        mavenCentral()
        maven {
            url = "https://jitpack.io"
        }
        maven {
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "com.github.GregTechCE:ForgeGradle:FG_2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.0"
group = "mod.alson.loggerslayer"
archivesBaseName = "loggerslayer"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

sourceSets {
    main
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    mappings = "stable_39"
    makeObfSourceJar = false

    def args = [
            "-Dfml.coreMods.load=mod.alson.loggerslayer.LoggerSlayerPlugin",
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true",
            "-Dmixin.debug.export=true"
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

mixin {
    add sourceSets.main, "mixins.loggerslayer.refmap.json"
}

repositories {
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {

    deobfCompile('org.spongepowered:mixin:0.8.1-SNAPSHOT')
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//Create a minimal jar that does not contain shadowed mixin things
jar {
    classifier "thin"
    manifest.attributes(
            "FMLCorePlugin": "mod.alson.loggerslayer.LoggerSlayerPlugin",
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.loggerslayer.json',
            'FMLCorePluginContainsFMLMod': 'true'
    )
}

//Shadow in Sponge mixins and other stuff, in case someone is not already providing it
shadowJar {
    classifier ""
    exclude "LICENSE.txt", "dummyThing"
}

//Artifact both the non-shadowed and shadowed version of the jar
artifacts {
    archives jar
    archives shadowJar
}